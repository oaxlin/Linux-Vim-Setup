#!/usr/bin/perl
use strict;
use warnings;
use File::Basename;

my $nb_errors = 0;

# run checks on each file
my $files = {
    perl => [],
    html => [],
    sql  => [],
    # other file types can be expanded/checked if needed
};

my $git_root = `git rev-parse --show-toplevel`;
chomp $git_root;
$ENV{'PERL5LIB'} ||= "$git_root/lib";

if (qx{ls ~/.git-templates/hooks/pre-commit}) {
    # running the user file would potentially be a security risk.  so instead let's just take a peek to make sure they are the same
    qx{which md5 2>&1 > /dev/null};
    my $prog = $? ? 'md5sum' : 'md5 -r'; # linux vs mac
    my ($me) = qx{$prog $0} =~ /^([^ ]+)/;
    my ($you) = qx{$prog ~/.git-templates/hooks/pre-commit} =~ /^([^ ]+)/;
    if ($you ne $me) {
        print "###############################################################\n";
        print "##\n";
        print "##   WARING: Your pre-commit hook appears to be out of date\n";
        print "##\n";
        print "###############################################################\n";
        $nb_errors++;
    }
}

for my $filepath (`git diff --cached --name-only`) {
    chomp $filepath;
    my @file_infos = fileparse( $filepath, qr/\.[^.]*/ );
    my $head = qx{/usr/bin/head -n 1 $filepath};
    $nb_errors++ if ($?); # just make sure the backtick script ran successfully

    if ( $file_infos[2] =~ /^\.(pl|cgi|pm)$/ || $head =~ /^(package [^;]+;|\#\!.*perl)$/ ) {
        push @{$files->{'perl'}}, $filepath;
    }
    if ( $file_infos[2] =~ /^\.(sql)$/) {
        push @{$files->{'sql'}}, $filepath;
    }
    if ( $file_infos[2] =~ /^\.(html?)$/) {
        push @{$files->{'html'}}, $filepath;
    }
}

if (my $num = scalar @{$files->{'perl'}}) {
    print "Checking perl -c ($num files)\n";
    my @passed;
    foreach my $file (@{$files->{'perl'}}) {
        my $out = qx{perl -c $file 2>&1};
        if ($?) {
            print $out;
            $nb_errors++;
        } else {
            push @passed, $file;
        }
    }
    if ($num = scalar @passed) {
        # perlcritic checks on something that failes "perl -c" is kinda pointless
        print "Checking perlcritic ($num files)\n";
        my $out = qx{perlcritic --quiet @passed};
        if ($?) {
            print $out;
            $nb_errors++;
        }
    }
}

if (my $num = scalar @{$files->{'html'}}) {
    #TODO
}

if (my $num = scalar @{$files->{'sql'}}) {
    #TODO
}

if ( $nb_errors ) {
    print "Aborting commit due to pre-commit hook failure.  You can still commit with the --no-verify argument.\n";
    exit 1;
}
exit 0;
